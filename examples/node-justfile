# SPDX-FileCopyrightText: 2025 Digg - Agency for Digital Government
#
# SPDX-License-Identifier: CC0-1.0

# Example justfile for a Node/TypeScript project using devbase-check

devtools_repo := env("DEVBASE_CHECK_REPO", "https://github.com/diggsweden/devbase-check")
devtools_dir := env("XDG_DATA_HOME", env("HOME") + "/.local/share") + "/devbase-check"
lint := devtools_dir + "/linters"
node_lint := devtools_dir + "/linters/node"
colors := devtools_dir + "/utils/colors.sh"

# Color variables
CYAN_BOLD := "\\033[1;36m"
GREEN := "\\033[1;32m"
BLUE := "\\033[1;34m"
NC := "\\033[0m"

# ==================================================================================== #
# DEFAULT
# ==================================================================================== #

default:
    @printf "{{CYAN_BOLD}} My Node Project{{NC}}\n\n"
    @printf "Quick start: {{GREEN}}just setup-devtools{{NC}} | {{BLUE}}just verify{{NC}}\n\n"
    @just --list --unsorted

# ==================================================================================== #
# SETUP
# ==================================================================================== #

# ▪ Setup devtools (clone or update)
[group('setup')]
setup-devtools:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ -d "{{devtools_dir}}" ]]; then
        # Try to check for updates, warn if no network connection
        if ! git -C "{{devtools_dir}}" fetch --tags --depth 1 --quiet 2>/dev/null; then
            printf "\033[0;33m! Could not check for updates (no network connection)\033[0m\n"
        elif [[ -f "{{devtools_dir}}/scripts/setup.sh" ]]; then
            "{{devtools_dir}}/scripts/setup.sh" "{{devtools_repo}}" "{{devtools_dir}}"
        fi
    else
        printf "Cloning devbase-check to %s...\n" "{{devtools_dir}}"
        mkdir -p "$(dirname "{{devtools_dir}}")"
        git clone --depth 1 "{{devtools_repo}}" "{{devtools_dir}}"
        git -C "{{devtools_dir}}" fetch --tags --depth 1 --quiet
        latest=$(git -C "{{devtools_dir}}" describe --tags --abbrev=0 origin/main 2>/dev/null || echo "")
        if [[ -n "$latest" ]]; then
            git -C "{{devtools_dir}}" fetch --depth 1 origin tag "$latest" --quiet
            git -C "{{devtools_dir}}" checkout "$latest" --quiet"
        fi
        printf "Installed devbase-check %s\n" "${latest:-main}"
    fi

# Check required tools
[group('setup')]
check-tools: _ensure-devtools
    @{{devtools_dir}}/scripts/check-tools.sh --check-devtools mise git just node npm rumdl yamlfmt actionlint gitleaks shellcheck shfmt conform reuse

# Install Node dependencies
[group('setup')]
deps-install:
    npm install

# ==================================================================================== #
# VERIFY
# ==================================================================================== #

# ▪ Run all checks
[group('verify')]
verify: _ensure-devtools check-tools lint-all test
    #!/usr/bin/env bash
    source "{{colors}}"
    just_success "All checks passed"

# ==================================================================================== #
# LINT
# ==================================================================================== #

# ▪ Run all linters (base + Node)
[group('lint')]
lint-all: _ensure-devtools lint-node
    #!/usr/bin/env bash
    source "{{colors}}"
    just --justfile {{devtools_dir}}/justfile lint-base
    just_success "All linting checks completed"

# Individual base linters (can be run separately)
[group('lint')]
lint-commits:
    @{{lint}}/commits.sh

[group('lint')]
lint-secrets:
    @{{lint}}/secrets.sh

[group('lint')]
lint-yaml:
    @{{lint}}/yaml.sh check

[group('lint')]
lint-markdown:
    @{{lint}}/markdown.sh check

[group('lint')]
lint-shell:
    @{{lint}}/shell.sh

[group('lint')]
lint-shell-fmt:
    @{{lint}}/shell-fmt.sh check

[group('lint')]
lint-actions:
    @{{lint}}/github-actions.sh

[group('lint')]
lint-license:
    @{{lint}}/license.sh

[group('lint')]
lint-container:
    @{{lint}}/container.sh

# Lint Node code (all: eslint, prettier, types)
[group('lint')]
lint-node:
    @{{node_lint}}/lint.sh

# Lint Node - eslint only
[group('lint')]
lint-node-eslint:
    @{{node_lint}}/eslint.sh

# Lint Node - prettier check only
[group('lint')]
lint-node-format:
    @{{node_lint}}/format.sh check

# Lint Node - type checking only
[group('lint')]
lint-node-ts-types:
    @{{node_lint}}/types.sh

# ==================================================================================== #
# LINT-FIX
# ==================================================================================== #

# ▪ Fix all auto-fixable issues
[group('lint-fix')]
lint-fix: _ensure-devtools lint-yaml-fix lint-markdown-fix lint-shell-fmt-fix lint-node-format-fix
    #!/usr/bin/env bash
    source "{{colors}}"
    just_success "All auto-fixes completed"

[group('lint-fix')]
lint-yaml-fix:
    @{{lint}}/yaml.sh fix

[group('lint-fix')]
lint-markdown-fix:
    @{{lint}}/markdown.sh fix

[group('lint-fix')]
lint-shell-fmt-fix:
    @{{lint}}/shell-fmt.sh fix

# Fix Node formatting
[group('lint-fix')]
lint-node-format-fix:
    @{{node_lint}}/format.sh fix

# ==================================================================================== #
# TEST
# ==================================================================================== #

# ▪ Run tests
[group('test')]
test:
    #!/usr/bin/env bash
    source "{{colors}}"
    just_header "Running tests" "npm test"
    npm test
    just_success "Tests passed"

# ==================================================================================== #
# BUILD
# ==================================================================================== #

# ▪ Build project
[group('build')]
build:
    #!/usr/bin/env bash
    source "{{colors}}"
    just_header "Building" "npm run build"
    npm run build
    just_success "Build completed"

# Clean build artifacts
[group('build')]
clean:
    #!/usr/bin/env bash
    source "{{colors}}"
    just_header "Cleaning" "rm -rf dist node_modules"
    rm -rf dist node_modules
    just_success "Clean completed"

# ==================================================================================== #
# INTERNAL
# ==================================================================================== #

[private]
_ensure-devtools:
    @just setup-devtools
